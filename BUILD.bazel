# SPDX-License-Identifier: BSD-3-Clause
# Copyright (c) 2025-2025, The OpenROAD Authors

load("@bazel_skylib//rules:common_settings.bzl", "string_flag")
load("//bazel:tcl_encode_or.bzl", "tcl_encode")
load("//bazel:tcl_wrap_cc.bzl", "tcl_wrap_cc")
load("//bazel:python_wrap_cc.bzl", "python_wrap_cc")

package(
    features = [
        "-parse_headers",
        "layering_check",
        # TODO(b/299593765): Fix strict ordering.
        "-libcxx_assertions",
    ],
)

exports_files([
    "LICENSE",
    "src/Design.i",
    "src/Exception.i",
])

string_flag(
    name = "platform",
    build_setting_default = "cli",
    values = [
        "cli",
        "gui",
    ],
)

config_setting(
    name = "platform_cli",
    flag_values = {
        ":platform": "cli",
    },
)

config_setting(
    name = "platform_gui",
    flag_values = {
        ":platform": "gui",
    },
)

# TODO: once project is properly decomposed, we don't
# need these blanked dependencies in multiple places anymore.
OPENROAD_LIBRARY_DEPS = [
    ":openroad_version",
    ":ord",
    "//src/ant",
    "//src/ant:ui",
    "//src/cgt",
    "//src/cts",
    "//src/cts:ui",
    "//src/cut",
    "//src/dbSta",
    "//src/dbSta:ui",
    "//src/dft",
    "//src/dft:ui",
    "//src/dpl",
    "//src/dpl:ui",
    "//src/drt",
    "//src/drt:ui",
    "//src/dst",
    "//src/dst:ui",
    "//src/est",
    "//src/est:ui",
    "//src/exa",
    "//src/exa:ui",
    "//src/fin",
    # "//src/fin:ui",
    "//src/gpl",
    # "//src/gpl:ui",
    "//src/grt",
    "//src/grt:ui",
    "//src/ifp",
    "//src/ifp:ui",
    "//src/mpl",
    "//src/mpl:ui",
    "//src/odb",
    "//src/odb:ui",
    "//src/pad",
    # "//src/pad:ui",
    "//src/par",
    "//src/par:ui",
    "//src/pdn",
    # "//src/pdn:ui",
    "//src/ppl",
    # "//src/ppl:ui",
    "//src/psm",
    # "//src/psm:ui",
    "//src/rcx",
    "//src/rcx:ui",
    "//src/rmp",
    "//src/rmp:ui",
    "//src/rsz",
    "//src/rsz:ui",
    "//src/stt",
    "//src/stt:ui",
    "//src/tap",
    "//src/tap:ui",
    "//src/upf",
    "//src/upf:ui",
    "//src/utl",
    "//src/utl:ui",
    "@edu_berkeley_abc//:abc-lib",
] + select(
    {
        ":platform_gui": ["//src/gui:gui_qt"],
        ":platform_cli": ["//src/gui"],
    },
)

OPENROAD_COPTS = [
    "-Wno-error",
    "-Wall",
    "-Wextra",
    "-pedantic",
    "-Wno-cast-qual",  # typically from TCL swigging
    "-Wno-missing-braces",  # typically from TCL swigging
    "-Wredundant-decls",
    "-Wformat-security",
    "-Wno-sign-compare",
    "-Wno-unused-parameter",
    "-fopenmp",
]

OPENROAD_DEFINES = [
    "OPENROAD_GIT_DESCRIBE=\\\"bazel-build\\\"",
    "BUILD_TYPE=\\\"$(COMPILATION_MODE)\\\"",
    "GPU=false",
    "BUILD_PYTHON=true",
    "ENABLE_PYTHON3",
    "ABC_NAMESPACE=abc",
    "TCLRL_VERSION_STR=",
]

cc_binary(
    name = "openroad",
    srcs = [
        "src/Main.cc",
        "src/OpenRoad.cc",
        ":openroad_swig",
        ":openroad_swig-py",
        ":openroad_py",
        ":openroad_tcl",
        ":rmp_swig",
        ":rmp_swig-py",
        ":rmp_python",
        ":rmp_tcl",
        "//bazel:runfiles",
    ],
    copts = OPENROAD_COPTS,
    features = ["-use_header_modules"],
    malloc = "@tcmalloc//tcmalloc",
    visibility = ["//visibility:public"],
    deps = [
        ":openroad_lib",
        ":openroad_version",
        "//:ord",
        "//src/cut",
        "//src/gui",
        "//src/sta:opensta_lib",
        "//src/utl",
        "@boost.stacktrace",
        "@rules_cc//cc/runfiles",
        "@tk_tcl//:tcl",
        "@openroad_rules_python//python/cc:current_py_cc_headers",
        "@openroad_rules_python//python/cc:current_py_cc_libs",
    ],
)

GUI_BUILD_FLAGS = select(
    {
        ":platform_gui": ["BUILD_GUI=true"],
        ":platform_cli": ["BUILD_GUI=false"],
    },
)

cc_library(
    name = "openroad_lib_private",
    srcs = [
        "src/Design.cc",
        "src/Tech.cc",
        "src/Timing.cc",
    ],
    copts = OPENROAD_COPTS,
    defines = OPENROAD_DEFINES + GUI_BUILD_FLAGS,
    features = ["-use_header_modules"],
    includes = [
        "include",
    ],
    deps = OPENROAD_LIBRARY_DEPS + [
        "//src/sta:opensta_lib",
        "@tk_tcl//:tcl",
    ],
)

cc_library(
    name = "openroad_lib",
    srcs = [
        "src/Design.cc",
        "src/OpenRoad.cc",
        "src/Tech.cc",
        "src/Timing.cc",
        ":openroad_swig",
        ":openroad_swig-py",
        ":openroad_py",
        ":openroad_tcl",
        ":rmp_swig",
        ":rmp_swig-py",
        ":rmp_python",
        ":rmp_tcl",
    ] + glob([
        "src/rmp/src/*.cpp",
        "src/rmp/src/*.h",
    ]),
    hdrs = glob(["src/rmp/include/rmp/*.h"]),
    copts = OPENROAD_COPTS,
    defines = OPENROAD_DEFINES + GUI_BUILD_FLAGS,
    features = ["-use_header_modules"],
    includes = [
        "include",
    ],
    visibility = ["//visibility:public"],
    deps = OPENROAD_LIBRARY_DEPS + [
        "//src/sta:opensta_lib",
        "@boost.stacktrace",
        "@tk_tcl//:tcl",
    ],
)

cc_library(
    name = "ord",
    hdrs = [
        "include/ord/Design.h",
        "include/ord/InitOpenRoad.hh",
        "include/ord/OpenRoad.hh",
        "include/ord/Tech.h",
        "include/ord/Timing.h",
        "include/ord/Version.hh",
    ],
    includes = [
        "include",
        "include/ord",
    ],
    visibility = ["//:__subpackages__"],
)

cc_library(
    name = "openroad_version",
    hdrs = [
        ":OpenRoadVersion",
    ],
)

genrule(
    name = "OpenRoadVersion",
    srcs = [],
    outs = ["include/ord/Version.hh"],
    cmd = """echo -e '
#define OPENROAD_VERSION "HDL-HEAD"
#define OPENROAD_GIT_SHA1 "HEAD"
' > \"$@\"
    """,
)

tcl_encode(
    name = "openroad_tcl",
    srcs = ["//src/sta:tcl_util"] + [
        "src/Metrics.tcl",
        "src/OpenRoad.tcl",
    ],
    char_array_name = "ord_tcl_inits",
    namespace = "ord",
)

tcl_wrap_cc(
    name = "openroad_swig",
    srcs = [
        "src/OpenRoad.i",
        ":error_swig",
    ],
    module = "ord",
    namespace_prefix = "ord",
    root_swig_src = "src/OpenRoad.i",
    swig_includes = [
        "src",
    ],
)

python_wrap_cc(
    name = "openroad_swig-py",
    srcs = [
        "src/OpenRoad-py.i",
        ":error_swig-py",
        "include/ord/Tech.h",
        "include/ord/Design.h",
        "include/ord/Timing.h",
    ],
    module = "ord_py",
    root_swig_src = "src/OpenRoad-py.i",
    swig_includes = [
        "include",
        "src",
    ],
)

tcl_encode(
    name = "openroad_py",
    srcs = [
        ":openroad_swig-py",
    ],
    char_array_name = "ord_py_python_inits",
    namespace = "ord",
)

tcl_wrap_cc(
    name = "rmp_swig",
    srcs = [
        "src/rmp/src/rmp.i",
        ":error_swig",
        "//src/sta:sta_swig_files",
    ],
    module = "rmp",
    namespace_prefix = "rmp",
    root_swig_src = "src/rmp/src/rmp.i",
    swig_includes = [
        "src/rmp/src",
        "src/sta",
    ],
)

python_wrap_cc(
    name = "rmp_swig-py",
    srcs = [
        "src/rmp/include/rmp/blif.h",
        "src/rmp/include/rmp/Restructure.h",
        "src/rmp/src/rmp-py.i",
        ":error_swig-py",
        "//src/sta:sta_swig_files",
    ],
    module = "rmp_py",
    root_swig_src = "src/rmp/src/rmp-py.i",
    swig_includes = [
        "src/rmp/include",
        "src/rmp/src",
        "src/sta",
    ],
)

tcl_encode(
    name = "rmp_python",
    srcs = [
        ":rmp_swig-py",
    ],
    char_array_name = "rmp_py_python_inits",
    namespace = "rmp",
)

filegroup(
    name = "error_swig",
    srcs = [
        "src/Exception.i",
    ],
    visibility = ["@//:__subpackages__"],
)

filegroup(
    name = "error_swig-py",
    srcs = [
        "src/Exception-py.i",
    ],
    visibility = ["@//:__subpackages__"],
)

filegroup(
    name = "design_swig",
    srcs = [
        "src/Design.i",
    ],
)
